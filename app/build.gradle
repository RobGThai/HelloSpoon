buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.stanfy.spoon:spoon-gradle-plugin:0.14.1'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

apply plugin: 'com.android.application'
apply plugin: 'spoon'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    defaultConfig {
        applicationId "com.robgthai.spoon.hellospoon"
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"

        testApplicationId "com.robgthai.spoon.hellospoon.test"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        testHandleProfiling true
        testFunctionalTest true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }

//    testOptions {
//        unitTests.returnDefaultValues = true
//    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile group: 'com.google.guava', name: 'guava', version: '15.0'

//    androidTestCompile ('com.android.support.test:testing-support-lib:0.1')
    androidTestCompile ('com.android.support.test.espresso:espresso-contrib:2.0') {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }
    androidTestCompile 'junit:junit:4.11'
    androidTestCompile 'com.squareup.spoon:spoon-client:1.1.2'
    androidTestCompile ('com.android.support.test.espresso:espresso-core:2.0')
    androidTestCompile ('com.squareup.burst:burst-junit4:1.0.2')

//    testCompile 'junit:junit:4.12'
//    testCompile ('com.android.support.test.espresso:espresso-core:2.0')
}

/**
 * Espresso Junit-dep dependencies work around. More info: https://github.com/square/burst/issues/48
 */
configurations.all {
    // Espresso depends on the now-obsolete junit-dep module. There's no need for it since we
    // already bring in the main junit module.
//    exclude group: 'junit', module: 'junit-dep'
    exclude module : 'junit-dep'

    resolutionStrategy {
        // Espresso depends on version 1.1 of hamcrest, and doesn't work with 1.3. JUnit relies on
        // hamcrest-core 1.3, but seems to work with 1.1. So we need to override gradle's default
        // resolution strategy and force 1.1 to be used.
        force 'org.hamcrest:hamcrest-core:1.1'
        force 'org.hamcrest:hamcrest-integration:1.1'
        force 'org.hamcrest:hamcrest-library:1.1'
    }
}

spoon {
    // for debug output
    debug = true

    baseOutputDir = file("$buildDir/outputs/reports/spoon/")

    if (project.hasProperty('spoonClassName')) {
        className = project.spoonClassName

        if (project.hasProperty('spoonMethodName')) {
            methodName = project.spoonMethodName
        }
    }
}
